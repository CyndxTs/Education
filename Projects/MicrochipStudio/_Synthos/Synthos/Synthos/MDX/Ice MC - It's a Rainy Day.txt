; By: Argedia & CyndxTs
; Declaracion de Constantes de Programa
.EQU NumNotas	=	14
.EQU Pos_S		=	0	; En este caso la posición es '0' porque está al inicio, pero esto obviamente puede variar..
.EQU Posiciones	=	0x0060
.EQU Duraciones	=	0x00C0

; Vectores de Programa
.CSEG
.ORG 0		RJMP	Synthos
.ORG 0x26	RJMP	ProcesarSemifusa
.ORG 0x30

; Almacenamiento de Notas Musicales en Flash
NotasMusicales:
.DW 0x0     ; OCR1(A|B) de S
.DW 0x0238  ; OCR1(A|B) de A5
.DW 0x0259  ; OCR1(A|B) de G5A5
.DW 0x027D  ; OCR1(A|B) de G5
.DW 0x02A3  ; OCR1(A|B) de F5G5
.DW 0x02CB  ; OCR1(A|B) de F5
.DW 0x02F6  ; OCR1(A|B) de E5
.DW 0x0323  ; OCR1(A|B) de D5E5
.DW 0x0353  ; OCR1(A|B) de D5
.DW 0x0385  ; OCR1(A|B) de C5D5
.DW 0x03BB  ; OCR1(A|B) de C5
.DW 0x03F4  ; OCR1(A|B) de B4
.DW 0x0430  ; OCR1(A|B) de A4B4
.DW 0x0470  ; OCR1(A|B) de A4

; Almacenamiento de las Secciones Musicales en Flash
SeccionesMusicales:
; SEC 1
.DB 0b01000011, 0b01000101
.DB 0b00000101, 0b00000101
.DB 0b01101011, 0b01101101
.DB 0b00000101, 0b00000101
.DB 0b01000011, 0b01000101
.DB 0b00000101, 0b00000101
.DB 0b00110011, 0b00110101
.DB 0b00000101, 0b00000101
.DB 0b00101011, 0b00101101
.DB 0b00000101, 0b00000101
.DB 0b00110011, 0b00110101
.DB 0b00000101, 0b00000101
.DB 0b00101011, 0b00101101
.DB 0b00000101, 0b00000101
.DB 0b00011011, 0b00011101
.DB 0b00000101, 0b00000101
.DB 0b00000100, 0b00000110
.DB 0b00000101, 0b00000101
; SEC 2
.DB 0b00011011, 0b00011101
.DB 0b00000101, 0b00000101
.DB 0b00001011, 0b00001101
.DB 0b00000101, 0b00000101
.DB 0b00011011, 0b00011101
.DB 0b00000101, 0b00000101
.DB 0b00101011, 0b00101101
.DB 0b00000101, 0b00000101
.DB 0b00000100, 0b00000110
.DB 0b00000101, 0b00000101
.DB 0b00110011, 0b00110101
.DB 0b00000101, 0b00000101
.DB 0b00000011, 0b00000100
.DB 0b00000100, 0b00000110
; SEC 3
.DB 0b00011011, 0b00011101
.DB 0b00000101, 0b00000101
.DB 0b00000100, 0b00000110
.DB 0b00000101, 0b00000101
.DB 0b00011011, 0b00011101
.DB 0b00000101, 0b00000101
.DB 0b00101011, 0b00101101
.DB 0b00000101, 0b00000101
.DB 0b00000100, 0b00000110
.DB 0b00000101, 0b00000101
.DB 0b00110011, 0b00110101
.DB 0b00000101, 0b00000101
.DB 0b00000011, 0b00000100
.DB 0b00000100, 0b00000110
; SEC 5
.DB 0b00000001, 0b00000001
.DB 0b00000001, 0b00000101
; SEC 4
.DB 0b01000011, 0b00000110
.DB 0b00110011, 0b00000110
.DB 0b00101011, 0b00000110
.DB 0b01100011, 0b00000110
.DB 0b01101011, 0b00000110

; Welcome to Synthos!
Synthos:
	; Inicializacion de Elementos [STACK]
	LDI		R16, high(RAMend)
	OUT		SPH, R16
	LDI		R16, low(RAMend)
	OUT		SPL, R16
	; Inicializacion de Elementos [I/O] {A (Entrada)}
	CLR		R16
	OUT		ddrA, R16
	; Inicializacion de Elementos [I/O] {B (Salida), C (Salida), D (Salida)}
	SER		R16
	OUT		ddrB, R16
	OUT		ddrC, R16
	OUT		ddrD, R16
	; Inicialización de Elementos [Visualizador de Audio]
	RCALL	CargarVisualizador
	; Inicializacion de Elementos [Patrón de Secciones]
	RCALL	CargarPatronDeSecciones
	; Activación de Programa
	EsperarActivacion:
	IN		R16,pinA
	CPI		R16,0
   	BREQ	EsperarActivacion
	; Inicialización de Elementos [Punteros a Datos de Sección]
	RCALL	InicializarPunterosDeSeccion
	; Inicializacion de Elementos [Timer Counter 0]
	; TCCR0 [CTC][N = 64]
	LDI		R16, (1 << WGM01 | 0 << WGM00 | 0<<CS02 | 1 << CS01 | 1 << CS00)
	OUT		TCCR0, R16
	; OCR0
	LDI		R16, 244
	OUT		OCR0, R16
	; TIMSK
	LDI		R16, (1 << OCIE0)
	OUT		TIMSK, R16
	; Inicializacion de Elementos [Timer Counter 1]
	; TCCR1A [CTC][N = 1]
	LDI		R16, (0 << WGM11 | 0 << WGM10 | 0 << COM1A1 | 1 << COM1A0)
	OUT		TCCR1A, R16
	; TCCR1B [CTC][N = 1]
	LDI		R16, (0 << WGM13 | 1 << WGM12 | 0 << CS12 | 0 << CS11 | 1 << CS10)
	OUT		TCCR1B, R16
	; OCR1A
	CLR		R16
	OUT		OCR1AH, R16
	OUT		OCR1AL, R16
	; Inicializacion de Elementos [Registros Auxiliares]
							; R16 = Registro Multipropósito
	CLR		R17				; R17 = Medida de Seccion
	LDI		R18, 1			; R18 = Duracion de Nota
							; R19 = Registro Multipropósito
							; R20 = Medida de Segmentación de Visualizador
							; R21 = Posición de Nota
							; R22 =	Valor Acumulado en Y [Matriz]
	CLR		R23				; R23 = Señal de Fin de Reproducción // Lo haría simplemente con 'BRIE' o 'BRID', pero por algun motivo no funciona .-.
	; Inicializacion de Elementos [Interrupciones]
	SEI
	; Dormammu he venido para negociar.jpg
	Dormammu:
	SBRS	R23, 0	
	RJMP	Dormammu
	EsperarDesactivacion:
	IN		R16, pinA
	CPI		R16, 0
	BRNE	EsperarDesactivacion
	RJMP	EsperarActivacion

; Inicialización de Punteros de Datos de Sección
InicializarPunterosDeSeccion:
	LDI		XH,	high(Posiciones)
	LDI		XL,	low(Posiciones)
	LDI		YH, high(Duraciones)
	LDI		YL, low(Duraciones)
RET

; Subrutina de Interrupcion por Semifusa
ProcesarSemifusa:
	; Validación por Duración de Nota Anterior
	DEC		R18
	BRNE	VisualizarNota
	; Validación por Duración de Sección Anterior
	CPI		R17, 0
	BRNE	CargarProximaNota
	; Carga de Datos de Próxima Sección
	CargarProximaSeccion:
	LD		R17, Y+
	CPI		R17, 0 
	BRNE	PosicionarSeccion
	RCALL	FinalizarReproduccion
	RJMP	FinDeSemifusa
	; Posicionamiento en Sección Cargada
	PosicionarSeccion:
	LDI		ZH, high(2*SeccionesMusicales)
	LDI		ZL, low(2*SeccionesMusicales)
	LD		R16, X+
	ADD		ZL, R16
	CLR		R16
	ADC		ZH, R16
	; Carga de Datos de Próxima Nota
	CargarProximaNota:
   	LPM		R16, Z+
	LDI		R18, 0b00100000
	LDI		R19, 0b00000111
	AND		R19, R16
	; Cálculo de Duración de Nota
	CalcularDuracion:
	DEC		R19
	BREQ	CalcularPosicion
	LSR		R18
	RJMP	CalcularDuracion
	; Cálculo de Posición de Nota
	CalcularPosicion:
	LSR		R16
	LSR		R16
	LSR		R16
	MOV		R19, R16
	LSL		R16
	CP		R21, R19
	BREQ	PosicionarNota
	MOV		R21, R19
	LDI		R22, 1
	; Posicionamiento en Nota Cargada
	PosicionarNota:
	PUSH	ZL
	PUSH	ZH
	LDI		ZH, high(2*NotasMusicales)
	LDI		ZL, low(2*NotasMusicales)
	ADD		ZL, R16
	CLR		R16
	ADC		ZH, R16
	; Reproducción de Nota
	ReproducirNota:
	LPM		R16, Z+
	LPM		R19, Z+
	OUT		OCR1AH, R19
	OUT		OCR1AL, R16
	DEC		R17
	POP		ZH
	POP		ZL
	; Visualización de Nota
	VisualizarNota:
	SBRS	R18, 0
	RCALL	ActualizarVisualizador
	; Fin de Procesamiento de Semifusa
	FinDeSemifusa:
RETI

; Subrutina de Visualización de Nota en Matriz LED
ActualizarVisualizador:
	; Validación de por Nueva Posición de Nota
	CPI		R22, 1
	BRNE	Actualizar_Y
	LDI		R16, 0b00000001
	CLR		R19
	; Validación por Nota de 'Silencio'
	CPI		R21, Pos_S
	BRNE	CalcularSegmento
	OUT		portC, R19
	JMP		FinDeVisualizacion
	; Cálculo de posición de Segmento [Posición X [Matriz]]
	CalcularSegmento:
	CP		R19, R21
	BRGE	Actualizar_X
	ADD		R19, R20
	LSL		R16
	JMP		CalcularSegmento
	; Actualización de Posición de Segmento [Posición X [Matriz]]
	Actualizar_X:
	COM		R16
	OUT		portB, R16
	; Actualización de Posición Y de Segmento
	Actualizar_Y:
	OUT		portC, R22
	LSL		R22
	INC		R22
	FinDeVisualizacion:
RET

; Subrutina de Finalización de Reproducción de Audio
FinalizarReproduccion:
	CLI
	LDI		R16, (0<<WGM11 | 0<<WGM10 | 0<<COM1A1 | 0<<COM1A0 | 0<<COM1B1 | 0<<COM1B0)
	OUT		TCCR1A, R16
	LDI		R16, (0<<WGM13 | 1<<WGM12 | 0<<CS12 | 0<<CS11 | 1<<CS10)
	OUT		TCCR1B, R16
	CLR		R16
	OUT		TCCR0, R16
	OUT		OCR0, R16
	OUT		TIMSK, R16
	OUT		portC, R16
	COM		R16
	OUT		portB, R16
	LDI		R23, 1
RET

; Subrutina de Carga de Visualizador de Audio
CargarVisualizador:
	; Inicialización de Elementos
	LDI		R16, 8
	CLR		R19
	CLR		R20
	; Cálculo de Medida de Segmentación de Notas en Visualizador
	CalcularSegmentacion:
	CPI		R19, NumNotas
	BRGE	FinDeSegmentacion
	ADD		R19, R16
	INC		R20
	JMP		CalcularSegmentacion
	FinDeSegmentacion:
RET
; Subrutina de Carga de Patrón de Secciones
CargarPatronDeSecciones:
	; Inicialización de Elementos [Punteros a Datos de Sección]
	RCALL	InicializarPunterosDeSeccion
	; SEC 1 || 0 -> (36)
	LDI     R16, 0
	ST      X+, R16
	LDI     R16, 36
	ST      Y+, R16
	; SEC 2 || 36 -> (64)
	LDI     R16, 36
	ST      X+, R16
	LDI     R16, 28
	ST      Y+, R16
	; SEC 1 || 0 -> (36)
	LDI     R16, 0
	ST      X+, R16
	LDI     R16, 36
	ST      Y+, R16
	; SEC 3 || 64 -> (92)
	LDI     R16, 64
	ST      X+, R16
	LDI     R16, 28
	ST      Y+, R16
	; SEC 1 || 0 -> (36)
	LDI     R16, 0
	ST      X+, R16
	LDI     R16, 36
	ST      Y+, R16
	; SEC 2 || 36 -> (64)
	LDI     R16, 36
	ST      X+, R16
	LDI     R16, 28
	ST      Y+, R16
	; SEC 1 || 0 -> (36)
	LDI     R16, 0
	ST      X+, R16
	LDI     R16, 36
	ST      Y+, R16
	; SEC 3 || 64 -> (92)
	LDI     R16, 64
	ST      X+, R16
	LDI     R16, 28
	ST      Y+, R16
	; SEC 1 || 0 -> (36)
	LDI     R16, 0
	ST      X+, R16
	LDI     R16, 36
	ST      Y+, R16
	; SEC 2 || 36 -> (64)
	LDI     R16, 36
	ST      X+, R16
	LDI     R16, 28
	ST      Y+, R16
	; SEC 1 || 0 -> (36)
	LDI     R16, 0
	ST      X+, R16
	LDI     R16, 36
	ST      Y+, R16
	; SEC 3 || 64 -> (92)
	LDI     R16, 64
	ST      X+, R16
	LDI     R16, 28
	ST      Y+, R16
	; SEC 1 || 0 -> (36)
	LDI     R16, 0
	ST      X+, R16
	LDI     R16, 36
	ST      Y+, R16
	; SEC 2 || 36 -> (64)
	LDI     R16, 36
	ST      X+, R16
	LDI     R16, 28
	ST      Y+, R16
	; SEC 1 || 0 -> (36)
	LDI     R16, 0
	ST      X+, R16
	LDI     R16, 36
	ST      Y+, R16
	; SEC 5 || 92 -> (96)
	LDI     R16, 92
	ST      X+, R16
	LDI     R16, 4
	ST      Y+, R16
	; SEC 1 || 0 -> (36)
	LDI     R16, 0
	ST      X+, R16
	LDI     R16, 36
	ST      Y+, R16
	; SEC 2 || 36 -> (64)
	LDI     R16, 36
	ST      X+, R16
	LDI     R16, 28
	ST      Y+, R16
	; SEC 1 || 0 -> (36)
	LDI     R16, 0
	ST      X+, R16
	LDI     R16, 36
	ST      Y+, R16
	; SEC 3 || 64 -> (92)
	LDI     R16, 64
	ST      X+, R16
	LDI     R16, 28
	ST      Y+, R16
	; SEC 1 || 0 -> (36)
	LDI     R16, 0
	ST      X+, R16
	LDI     R16, 36
	ST      Y+, R16
	; SEC 2 || 36 -> (64)
	LDI     R16, 36
	ST      X+, R16
	LDI     R16, 28
	ST      Y+, R16
	; SEC 1 || 0 -> (36)
	LDI     R16, 0
	ST      X+, R16
	LDI     R16, 36
	ST      Y+, R16
	; SEC 3 || 64 -> (92)
	LDI     R16, 64
	ST      X+, R16
	LDI     R16, 28
	ST      Y+, R16
	; Marcador de Final
	LDI     R16, 0
	ST      X+, R16
	LDI     R16, 0
	ST      Y+, R16
RET
